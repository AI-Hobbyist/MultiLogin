plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    mavenCentral()
    maven { url = 'https://nexus.ksnb.fun/repository/multilogin/' }
    maven { url = 'https://repo.opencollab.dev/main' }
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation project(":api")
    implementation project(":flows")
    implementation project(":loader")

    compileOnly fileTree(dir: 'libraries', include: ['*.jar'])
    compileOnly 'io.netty:netty-all:4.1.80.Final'

    compileOnly 'net.minecrell:terminalconsoleappender:1.3.0'

//    下面是用来补充proxy引用的 为自编译库 如果不可用清参照libraries下README.MD
    compileOnly 'com.velocitypowered:velocity-proxy:3.2.0'

    compileOnly 'org.geysermc.geyser:api:2.1.0-SNAPSHOT'

    compileOnly 'com.velocitypowered:velocity-api:3.1.0'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.0'
}


shadowJar {
    archiveBaseName.set('MultiLogin-Velocity')

    if (System.getProperty("env", "AUTO") == "final") {
        archiveVersion.set(project.plugin_version as String)
    } else {
        String ver = "Build_" + version
        archiveVersion.set(ver)
    }
    archiveClassifier.set("")
    from {
        project(':core').tasks.shadowJar.archiveFile
    }
    from {
        project(':velocity:injector').tasks.shadowJar.archiveFile
    }
}


artifacts {
    archives shadowJar
}